/*
 * SPDX-License-Identifier:    MIT
 */

pragma solidity 0.8.10;

import "@openzeppelin/contracts-upgradeable/token/ERC20/extensions/ERC20VotesUpgradeable.sol";
import "./../majority/MajorityVoting.sol";

/// @title A component for ERC-20 token voting
/// @author Giorgi Lagidze, Samuel Furter - Aragon Association - 2021-2022
/// @notice The majority voting implementation using an ERC-20 token
/// @dev This contract inherits from `MajorityVoting` and implements the `IMajorityVoting` interface
contract ERC20Voting is MajorityVoting {
    bytes4 internal constant ERC20_VOTING_INTERFACE_ID = MAJORITY_VOTING_INTERFACE_ID ^ this.getVotingToken.selector;

    ERC20VotesUpgradeable private votingToken;

    /// @notice Initializes the component
    /// @dev This is required for the UUPS upgradability pattern
    /// @param _dao The IDAO interface of the associated DAO
    /// @param _gsnForwarder The address of the trusted GSN forwarder required for meta transactions
    /// @param _participationRequiredPct The minimal required participation in percent.
    /// @param _supportRequiredPct The minimal required support in percent.
    /// @param _minDuration The minimal duration of a vote
    /// @param _token The ERC20 token used for voting
    function initialize(
        IDAO _dao,
        address _gsnForwarder,
        uint64 _participationRequiredPct,
        uint64 _supportRequiredPct,
        uint64 _minDuration,
        ERC20VotesUpgradeable _token
    ) public initializer {
        _registerStandard(ERC20_VOTING_INTERFACE_ID);
        __MajorityVoting_init(
            _dao,
            _gsnForwarder,
            _participationRequiredPct,
            _supportRequiredPct,
            _minDuration
        );

        votingToken = _token;
    }

    /// @notice getter function for the voting token
    /// @dev public function also useful for registering interfaceId and for distinguishing from majority voting interface 
    /// @return ERC20VotesUpgradeable the token used for voting 
    function getVotingToken() public view returns(ERC20VotesUpgradeable){
        return votingToken;
    }

    /// @notice Returns the version of the GSN relay recipient
    /// @dev Describes the version and contract for GSN compatibility
    function versionRecipient() external view virtual override returns (string memory) {
        return "0.0.1+opengsn.recipient.ERC20Voting";
    }

    /// @notice Create a new vote on this concrete implementation
    /// @param _proposalMetadata The IPFS hash pointing to the proposal metadata
    /// @param _actions the actions that will be executed after vote passes
    /// @param _startDate state date of the vote. If 0, uses current timestamp
    /// @param _endDate end date of the vote. If 0, uses _start + minDuration
    /// @param _executeIfDecided Configuration to enable automatic execution on the last required vote
    /// @param _choice Vote choice to cast on creation
    function newVote(
        bytes calldata _proposalMetadata,
        IDAO.Action[] calldata _actions,
        uint64 _startDate,
        uint64 _endDate,
        bool _executeIfDecided,
        VoterState _choice
    ) external override returns (uint256 voteId) {
        uint64 snapshotBlock = getBlockNumber64() - 1;

        uint256 votingPower = votingToken.getPastTotalSupply(snapshotBlock);
        if (votingPower == 0) revert VotePowerZero();
        
        voteId = votesLength++;

        // calculate start and end time for the vote
        uint64 currentTimestamp = getTimestamp64();

        if (_startDate == 0) _startDate = currentTimestamp;
        if (_endDate == 0) _endDate = _startDate + minDuration;

        if(_endDate - _startDate <  minDuration || _startDate < currentTimestamp)
            revert VoteTimesForbidden({
                current: currentTimestamp,
                start: _startDate,
                end: _endDate,
                minDuration: minDuration
            });

        // create a vote.
        Vote storage vote_ = votes[voteId];
        vote_.startDate = _startDate;
        vote_.endDate = _endDate;
        vote_.supportRequiredPct = supportRequiredPct;
        vote_.participationRequiredPct = participationRequiredPct;
        vote_.votingPower = votingPower;
        vote_.snapshotBlock = snapshotBlock;

        unchecked {
            for (uint256 i = 0; i < _actions.length; i++) {
                vote_.actions.push(_actions[i]);
            }
        }

        emit StartVote(voteId, _msgSender(), _proposalMetadata);

        if (_choice != VoterState.None && canVote(voteId, _msgSender())) {
            _vote(voteId, _choice, _msgSender(), _executeIfDecided);
        }
    }

    /// @dev Internal function to cast a vote. It assumes the queried vote exists.
    /// @param _voteId voteId
    /// @param _choice Whether voter abstains, supports or not supports to vote.
    /// @param _executesIfDecided if true, and it's the last vote required, immediatelly executes a vote.
    function _vote(
        uint256 _voteId,
        VoterState _choice,
        address _voter,
        bool _executesIfDecided
    ) internal override {
        Vote storage vote_ = votes[_voteId];

        // This could re-enter, though we can assume the governance token is not malicious
        uint256 voterStake = votingToken.getPastVotes(_voter, vote_.snapshotBlock);
        VoterState state = vote_.voters[_voter];

        // If voter had previously voted, decrease count
        if (state == VoterState.Yea) {
            vote_.yea = vote_.yea - voterStake;
        } else if (state == VoterState.Nay) {
            vote_.nay = vote_.nay - voterStake;
        } else if (state == VoterState.Abstain) {
            vote_.abstain = vote_.abstain - voterStake;
        }

        // write the updated/new vote for the voter.
        if (_choice == VoterState.Yea) {
            vote_.yea = vote_.yea + voterStake;
        } else if (_choice == VoterState.Nay) {
            vote_.nay = vote_.nay + voterStake;
        } else if (_choice == VoterState.Abstain) {
            vote_.abstain = vote_.abstain + voterStake;
        }

        vote_.voters[_voter] = _choice;

        emit CastVote(_voteId, _voter, uint8(_choice), voterStake);

        if (_executesIfDecided && _canExecute(_voteId)) {
            _execute(_voteId);
        }
    }

    /// @dev Internal function to check if a voter can participate on a vote. It assumes the queried vote exists.
    /// @param _voteId The voteId
    /// @param _voter the address of the voter to check
    /// @return True if the given voter can participate a certain vote, false otherwise
    function _canVote(uint256 _voteId, address _voter) internal view override returns (bool) {
        Vote storage vote_ = votes[_voteId];
        return _isVoteOpen(vote_) && votingToken.getPastVotes(_voter, vote_.snapshotBlock) > 0;
    }
}
